// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.2.61'
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}
ext {
    // Android
    androidArchLifecycleVersion = '1.1.1'
    androidSupportLibVersion = '27.1.1'
    constraintLayoutVersion = '1.1.3'

    //anko
    ankoVersion = '0.10.7'

    timberVersion = '4.7.0'
    glideVersion = '4.7.1'

    // Network
    okhttpVersion = '3.10.0'
    retrofitVersion = '2.4.0'

    // Testing
    junitVersion = '4.12'

    depsWithVersion = [
            // Android
            "com.android.support:appcompat-v7"                  : androidSupportLibVersion,
            "com.android.support.constraint:constraint-layout"  : constraintLayoutVersion,
            "com.android.support:design"                        : androidSupportLibVersion,
            "android.arch.lifecycle:extensions"                 : androidArchLifecycleVersion,

            //Anko
            "org.jetbrains.anko:anko-commons"                   : ankoVersion,

            // Utils
            "com.jakewharton.timber:timber"                     : timberVersion,
            "com.github.bumptech.glide:glide"                   : glideVersion,
            "com.github.bumptech.glide:compiler"                : glideVersion,

            // Network
            "com.squareup.okhttp3:okhttp"                       : okhttpVersion,
            "com.squareup.retrofit2:retrofit"                   : retrofitVersion,
            "com.squareup.retrofit2:converter-gson"        : retrofitVersion,

            // Testing
            "junit:junit"                                       : junitVersion,
            "org.jetbrains.kotlin:kotlin-test-junit"            : kotlin_version,
    ]

    deps = { String key ->
        def version = depsWithVersion[key]
        if (version == null) throw new IllegalDependencyNotation("Version for library '$key' unknown. Update root build.gradle")
        "$key:${version}"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        maven { url "http://repo1.maven.org/maven2" }
        maven { url 'https://jitpack.io' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
